buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.6.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.43.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.14.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.11.0'
        classpath 'com.palantir.gradle.failure-reports:gradle-failure-reports:1.10.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.48.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.23.0'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.21.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:5.61.0'
        classpath 'de.undercouch:gradle-download-task:5.6.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.1.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.7.0'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.failure-reports'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

allprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.jakarta-package-alignment'
    version System.env.CIRCLE_TAG ?: gitVersion()
    group 'com.palantir.sls-packaging'

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }
}

subprojects {
    apply plugin: 'java-library'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror', '-Xlint:deprecation']
    }

    tasks.named("eclipse") {
        dependsOn "pluginUnderTestMetadata"
    }
}

javaVersions {
    libraryTarget = 11
}
