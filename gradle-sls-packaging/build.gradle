apply plugin: 'com.gradle.plugin-publish'
// java-gradle-plugin MUST be applied before publish-jar.gradle
apply plugin: 'java-gradle-plugin'
apply from: "${rootDir}/gradle/publish-jar.gradle"
apply plugin: 'groovy'
apply plugin: 'org.inferred.processors'
apply plugin: 'de.undercouch.download'

dependencies {
    compileOnly 'com.palantir.gradle.consistentversions:gradle-consistent-versions'
    testRuntime 'com.palantir.gradle.consistentversions:gradle-consistent-versions'

    compile gradleApi()
    compile project(':gradle-recommended-product-dependencies')
    compile 'com.google.guava:guava'
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    compile "com.fasterxml.jackson.datatype:jackson-datatype-guava"
    compile 'com.palantir.safe-logging:preconditions'

    compileOnly 'org.immutables:value::annotations'
    annotationProcessor 'org.immutables:value'

    testCompile gradleTestKit()
    testCompile 'com.netflix.nebula:nebula-test'
    testCompile 'org.awaitility:awaitility'
}


/**
 * Per default, Gradle TestKit excludes compileOnly dependencies resulting in ClassNotFoundExceptions when sls-packaging
 * tries to access 'gradle-consistent-versions' classes in integ tests e.g. in 'AssetDistributionPluginIntegrationSpec'.
 * https://discuss.gradle.org/t/testkit-somehow-excludes-compileonly-dependencies
 */
tasks.withType(PluginUnderTestMetadata).configureEach {
    pluginClasspath.from(configurations.testRuntime)
}

pluginBundle {
    website = 'https://github.com/palantir/sls-packaging'
    vcsUrl = 'https://github.com/palantir/sls-packaging'
    description = 'Creates a distributable package containing libraries and scripts necessary to easily deploy and run a Java project.'
    tags = ['distribution', 'application']

    plugins {
        javaServiceDistributionPlugin {
            id = 'com.palantir.sls-java-service-distribution'
            displayName = 'Creates SLS Java services distributions'
        }
        assetDistributionPlugin {
            id = 'com.palantir.sls-asset-distribution'
            displayName = 'Creates SLS asset distributions'
        }
        podDistributionPlugin {
            id = 'com.palantir.sls-pod-distribution'
            displayName = 'Creates SLS pod distributions'
        }
    }
}

def yourkitVersion = "2020.9-b411"
def yourkitFilename = "YourKit-JavaProfiler-${yourkitVersion}"

task downloadYourkitDist(type: Download) {
    src "https://www.yourkit.com/download/${yourkitFilename}.zip"
    dest buildDir
    tempAndMove true
    onlyIfModified true
}

task verifyYourkitDist(type: Verify, dependsOn: downloadYourkitDist) {
    src file("${buildDir}/${yourkitFilename}.zip")
    algorithm 'SHA-256'
    checksum '03e65eedf7bd42567b38dd48a57b801ea5b8a13e8de318eb4d5435f6c9fbed7d'
}

task extractYourkitAgent(type: Copy, dependsOn: verifyYourkitDist) {
    from zipTree(file("${buildDir}/${yourkitFilename}.zip"))
    include '**/linux-x86-64/libyjpagent.so'
    eachFile { path = name } // Flatten directory with single file
    into file("${buildDir}/resources/main/linux-x86-64")
    includeEmptyDirs = false // Cleanup empty Yourkit dist directories
}

tasks.idea.dependsOn extractYourkitAgent
tasks.compileJava.dependsOn extractYourkitAgent

publishPlugins.onlyIf {
    project.version ==~ /[0-9]+(\.[0-9]+)+(-rc[0-9]+)?(-alpha[0-9]+)?/
}

test {
    maxHeapSize = '1g'
}

// Configure the publishPlugins task
tasks.publish.dependsOn publishPlugins
project.ext.'gradle.publish.key' = System.env["GRADLE_KEY"]
project.ext.'gradle.publish.secret' = System.env["GRADLE_SECRET"]
