apply plugin: 'com.gradle.plugin-publish'
// java-gradle-plugin MUST be applied before publish-jar.gradle
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'groovy'
apply plugin: 'org.inferred.processors'
apply plugin: 'de.undercouch.download'

dependencies {
    compileOnly 'com.palantir.gradle.consistentversions:gradle-consistent-versions'
    testRuntimeOnly 'com.palantir.gradle.consistentversions:gradle-consistent-versions'

    api gradleApi()
    api project(':gradle-recommended-product-dependencies')
    api project(':gradle-sls-packaging-api')
    api 'com.google.guava:guava'
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    api "com.fasterxml.jackson.datatype:jackson-datatype-guava"
    api 'com.palantir.safe-logging:preconditions'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.palantir.gradle.failure-reports:gradle-failure-reports-exceptions'

    compileOnly 'org.immutables:value::annotations'
    annotationProcessor 'org.immutables:value'
    compileOnly 'com.palantir.gradle.auto-parallelizable:auto-parallelizable-annotations'
    annotationProcessor 'com.palantir.gradle.auto-parallelizable:auto-parallelizable'

    testImplementation gradleTestKit()
    testImplementation 'com.netflix.nebula:nebula-test'
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.rauschig:jarchivelib'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
}

gradlePlugin {
    website = 'https://github.com/palantir/sls-packaging'
    vcsUrl = 'https://github.com/palantir/sls-packaging'

    plugins {
        javaServiceDistributionPlugin {
            id = 'com.palantir.sls-java-service-distribution'
            displayName = 'Creates SLS Java services distributions'
            tags.set(['distribution', 'application'])
            implementationClass = 'com.palantir.gradle.dist.service.JavaServiceDistributionPlugin'
            description = 'Creates a distributable package containing libraries and scripts necessary to easily deploy and run a Java project.'
        }
        assetDistributionPlugin {
            id = 'com.palantir.sls-asset-distribution'
            displayName = 'Creates SLS asset distributions'
            tags.set(['distribution', 'application'])
            implementationClass= 'com.palantir.gradle.dist.asset.AssetDistributionPlugin'
            description = 'Creates a distributable package containing an SLS asset.'
        }
    }
}

publishPlugins.onlyIf {
    project.version ==~ /[0-9]+(\.[0-9]+)+(-rc[0-9]+)?(-alpha[0-9]+)?/
}

test {
    maxHeapSize = '1g'
    // required so nebula tests don't fail on stuff to be removed in gradle 9
    // of course, we are going to have to deal with those things sooner or later
    systemProperty('ignoreDeprecations', true)
}

// Configure the publishPlugins task
tasks.publish.dependsOn publishPlugins
project.ext.'gradle.publish.key' = System.env["GRADLE_KEY"]
project.ext.'gradle.publish.secret' = System.env["GRADLE_SECRET"]
